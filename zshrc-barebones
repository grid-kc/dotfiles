bindkey -v # Use vim keymap
# For some reason, vi-backward-delete-char doesn't allow you to use the
# backspace key to delete anything prior to where you begin inserting,
# so we rebind backspace to the standard backward-delete-char
bindkey -v '^?' backward-delete-char

setopt AUTO_CD # This lets you cd into a directory by just typing its name
setopt AUTO_PUSHD # Put every cd on the pushd stack
setopt EXTENDEDGLOB # zsh's globbing is AWESOME

unsetopt CLOBBER # Use >| to overwrite a file with output redirection

# ls colors, shamelessly stolen from oh-my-zsh
export LSCOLORS="Gxfxcxdxbxegedabagacad"
ls --color -d . &> /dev/null && alias ls='ls --color=tty' || alias ls='ls -G'

# ---
# History setup
# ---
export HISTSIZE=1000
export SAVEHIST=1000
export HISTFILE=$HOME/.history

# Don't overwrite history when starting a new session
setopt APPEND_HISTORY
# Write history incrementally, instead of on session end
setopt INC_APPEND_HISTORY
# Share history across sessions
setopt SHARE_HISTORY
# Don't keep duplicate commands around unnecessarily
setopt HIST_EXPIRE_DUPS_FIRST
# Don't find duplicate commands in history
setopt HIST_FIND_NO_DUPS
# Compact commands that have newlines in them
setopt HIST_REDUCE_BLANKS
# Don't put `history | grep` in my history
setopt HIST_NO_STORE
# No unnecessary information
unsetopt EXTENDED_HISTORY

# ---
# Initial .zsh dir setup
# ---
local zsh_dir=$HOME/.zsh
if [[ ! -d $zsh_dir ]]; then
  command mkdir $zsh_dir
fi
fpath=(~/.zsh $fpath)

# ---
# Git Completion
# ---
# Download git completions from github
local git_comp_url="https://raw.githubusercontent.com/git/git/master/contrib/completion"
for ext in zsh bash; do
  if [[ ! -f $zsh_dir/git-completion.$ext ]]; then
    command curl --silent "$git_comp_url/git-completion.$ext" \
        --output $zsh_dir/git-completion.$ext
  fi
done
# Install zsh completions
source $zsh_dir/git-completion.bash &>/dev/null
if [[ ! -L $zsh_dir/_git ]]; then
  command ln -s $zsh_dir/git-completion.zsh $zsh_dir/_git
fi

# ---
# Prompt customization
# ---
function git_branch() {
  local ref
  ref=$(command git symbolic-ref HEAD 2>/dev/null)\
    || ref=$(command git rev-parse --short HEAD 2>/dev/null)\
    || return
  echo "${ref#refs/heads/}"
}
# This is how we make it actually call the git_branch function when
# evaluating the prompt
setopt PROMPT_SUBST
export PS1='%F{green}%~%f%F{red}|%f%F{blue}$(git_branch)%f %# '

# '^[/' is how you search in 'vi' mode, but by default it executes the
# _history-complete-older function, which I don't use for anything. So I unbind
# it
bindkey -r '^[/'

# Initialize pyenv, if we have it
if (( $+commands[pyenv] )); then
  eval "$(pyenv init -)"
fi
